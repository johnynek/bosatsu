package Bosatsu/Char

def string_to_Char(s: String) -> Option[Char]:
  match s:
    case "$.{c}": Some(c)
    case _: None

str_to_char_tests = TestSuite("string_to_Char",
  [
    Assertion(string_to_Char("s") matches Some(.'s'), "s"),
    Assertion(string_to_Char("") matches None, "empty"),
    Assertion(string_to_Char("foo") matches None, "foo"),
    # test a 2 word character
    Assertion(string_to_Char("ğŸ‘‹") matches Some(.'ğŸ‘‹'), "foo"),
  ]
)

len_test = TestSuite("len tests",
  [
    Assertion(length_String("") matches 0, "empty"),
    Assertion(length_String("x") matches 1, "x"),
    Assertion(length_String("hello") matches 5, "hello"),
    Assertion(length_String("ğŸ‘‹") matches 1, "ğŸ‘‹"),
  ]
)

def last_String(s: String) -> Option[Char]:
  match s:
    case "": None
    case "${_}$.{l}": Some(l)

last_tests = TestSuite(
  "last_String",
  [
    Assertion(last_String("") matches None, "empty"),
    Assertion(last_String("x") matches Some(.'x'), "x"),
    Assertion(last_String("1234") matches Some(.'4'), "1234"),
    Assertion(last_String("ğŸ‘‹") matches Some(.'ğŸ‘‹'), "ğŸ‘‹"),
  ]
)

partition_tests = TestSuite("partition tests", [
  Assertion("foo".partition_String("f") matches Some(("", "oo")), "foo partition_String f"),
  Assertion("foo".partition_String("") matches None, "foo partition_String \"\""),
  Assertion("foo".partition_String("x") matches None, "foo partition_String x"),
  Assertion("foo".rpartition_String("o") matches Some(("fo", "")), "foo rpartition_String o"),
  Assertion("foo".rpartition_String("") matches None, "foo rpartition_String \"\""),
  Assertion("foo".rpartition_String("x") matches None, "foo rpartition_String x"),
])

match_tests = TestSuite("match tests",
  [
    Assertion("ğŸ‘‹ğŸ‘‹ğŸ‘‹" matches "ğŸ‘‹$.{_}ğŸ‘‹", "test matching 1"),
    Assertion("abcğŸ‘‹ğŸ‘‹ğŸ‘‹" matches "${_}ğŸ‘‹$.{_}ğŸ‘‹", "test matching 2"),
    Assertion("abcğŸ‘‹ğŸ‘‹ğŸ‘‹" matches "abc$.{_}ğŸ‘‹${_}", "test matching 2.5"),
    Assertion("abcğŸ‘‹ğŸ‘‹ğŸ‘‹" matches "abcğŸ‘‹${_}", "test matching 3"),
    Assertion("abcğŸ‘‹ğŸ‘‹ğŸ‘‹" matches "${_}cğŸ‘‹${_}", "test matching 4"),
    Assertion("abcğŸ‘‹ğŸ‘‹ğŸ‘‹" matches "${_}cğŸ‘‹${_}$.{_}", "test matching 5"),
    Assertion("abcğŸ‘‹ğŸ‘‹ğŸ‘‹" matches "${_}ğŸ‘‹", "test matching 6"),
  ])

def starts_with_foo(s): s matches "foo${_}"
def ends_with_foo(s): s matches "${_}foo"
def contains_foo(s): s matches "${_}foo${_}"
def contains_foo_bar(s): s matches "${_}foo${_}bar${_}"

glob_match_tests = TestSuite("glob_match_suites",
  [
    Assertion(starts_with_foo("foobar"), "starts_with_foo(foobar)"),
    Assertion(starts_with_foo("barfoo") matches False, "starts_with_foo(foobar)"),
    Assertion(ends_with_foo("foobar") matches False, "ends_with_foo(foobar)"),
    Assertion(ends_with_foo("barfoo"), "ends_with_foo(foobar)"),
    Assertion(contains_foo("barfoo"), "contains_foo(foobar)"),
    Assertion(contains_foo("barbar") matches False, "contains_foo(barbar)"),
    Assertion(contains_foo_bar("there is foo and bar"), "there is foo and bar"),
    Assertion(contains_foo_bar("there is foobar"), "there is foobar"),
    Assertion(contains_foo_bar("there is foo but not the other") matches False,
      "there is foo but not the other"),
  ])

tests = TestSuite("Char tests",
  [
    str_to_char_tests,
    len_test,
    last_tests,
    match_tests,
    glob_match_tests,
    partition_tests,
  ]
)
