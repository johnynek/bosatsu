package Parser

export Parser, empty, parse, flat_map, expect, one_of, map

struct Parser[a](parse: String -> Option[(String, a)])

empty = Parser(_ -> None)

def parse(p: Parser[a], str: String) -> Option[a]:
    Parser(fn) = p
    match fn(str):
        case Some((_, a)): Some(a)
        case None: None

def pure(item: a) -> Parser[a]: Parser(s -> Some((s, item)))

def expect(str: String) -> Parser[Unit]:
    def p(s): match s.partition_String(str):
        case Some(("", rest)): Some((rest, ()))
        case Some(_) | None: None

    Parser(p)

def map(p, fn):
    Parser(pfn) = p
    Parser(s -> match pfn(s):
        case Some((rest, a)): Some((rest, fn(a)))
        case None: None)

def flat_map(p: Parser[a], fn: a -> Parser[b]) -> Parser[b]:
    Parser(fa) = p
    def parse(s0):
        match fa(s0):
            case Some((s1, a)):
                Parser(fb) = fn(a)
                fb(s1)
            case None: None

    Parser(parse)

def one_of(ps: List[Parser[a]]) -> Parser[a]:
    recur ps:
        case []: empty
        case [p1]: p1
        case [Parser(headFn), *prest]:
            Parser(tailFn) = one_of(prest)
            def parse(s):
                match headFn(s):
                    case None: tailFn(s)
                    case notEmpty: notEmpty

            Parser(parse)

def then_parse(pa: Parser[a], pb: Parser[b]) -> Parser[(a, b)]:
    Parser(fa) = pa
    Parser(fb) = pb
    def parse(s0):
        match fa(s0):
            case Some((s1, a)):
                match fb(s1):
                    case Some((s2, b)): Some((s2, (a, b)))
                    case None: None
            case None: None

    Parser(parse)

def expect_int(i: Int) -> Parser[Int]:
  expect(int_to_String(i)).map(_ -> i)

digit = one_of([expect_int(i) for i in range(10)])

enum Nat: Z, S(n: Nat)

def to_Nat(i: Int) -> Nat:
  int_loop(i, Z, (i, nat) -> (i.sub(1), S(nat)))

enum Rec[a, b]: Cont(next_input: a), Done(output: b)

def recur_max(n: Nat, fn: a -> Rec[a, b], in: a) -> Option[b]:
    recur n:
        case Z: None
        case S(prev):
            match fn(in):
              case Cont(in1): recur_max(prev, fn, in1)
              case Done(out): Some(out)

def digits_n(at_most: Int) -> Parser[Int]:
    Parser(fn) = digit

    def loop(in: (String, Int)) -> Rec[(String, Int), Option[(String, Int)]]:
        (s0, acc0) = in
        first = acc0 matches -1
        match fn(s0):
            case Some((s1, d)):
                acc = 0 if first else acc0
                Cont((s1, acc.times(10).add(d)))
            case None:
                if first: Done(None)
                else: Done(Some((s0, acc0)))

    # We add 1 because we want to go beyond the digit
    # and see that we have fully parsed
    parseFn = in -> recur_max(S(to_Nat(at_most)), loop, in)
    Parser(s ->
        match parseFn((s, -1)):
            case Some(Some(a)): Some(a)
            case Some(None) | None: None)

tests = TestSuite("Parser tests", [
    Assertion(pure(1).parse("foo") matches Some(1), "pure"),
    Assertion(expect("fo").parse("foo") matches Some(()), "expect(fo)"),
    Assertion(expect("fo").then_parse(expect("o")).parse("foo") matches Some(_), "expect(fo)"),
    Assertion(digit.parse("2") matches Some(2), "digit.parse(2)"),
    Assertion(digit.parse("9") matches Some(9), "digit.parse(2)"),
    Assertion(digits_n(10).parse("4242") matches Some(4242), "digits_n(10).parse(4242)"),
    Assertion(digits_n(10).parse("4242") matches Some(4242), "digits_n(10).parse(4242)"),
    Assertion(digits_n(3).parse("4242") matches None, "digits_n(3).parse(4242)"),
    Assertion(digits_n(4).parse("4242") matches Some(4242), "digits_n(4).parse(4242)"),
  ])
