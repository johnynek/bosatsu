package RecordSet/Library

enum RowEntry[w]:
  REBool(value: w[Bool])
  REInt(value: w[Int])
  REString(value: w[String])

struct RecordField[t](name: String, to_entry: forall w: * -> *. w[t] -> RowEntry[w])
struct RecordValue[t](value: t)
struct RecordGetter[shape, t](
  field: shape[RecordField] -> RecordField[t],
  value: shape[RecordValue] -> RecordValue[t]
)
struct RecordRowEntry[w, t](row_entry: RowEntry[w])

struct RecordSet[shape](
  fields: shape[RecordField],
  rows: List[shape[RecordValue]],
  getters: shape[RecordGetter[shape]],
  traverse: forall w1: * -> *, w2: * -> *. shape[w1] -> (forall ss. w1[ss] -> w2[ss]) -> shape[w2],
  record_to_list: forall w: * -> *. shape[RecordRowEntry[w]] -> List[RowEntry[w]]
)

def get[shape: (* -> *) -> *, t](sh: shape[RecordValue], RecordGetter(_, getter): RecordGetter[shape, t]) -> t:
  RecordValue(result) = sh.getter()
  result

def create_field[shape: (* -> *) -> *, t](rf: RecordField[t], fn: shape[RecordValue] -> t):
  RecordGetter(_ -> rf, sh -> RecordValue(fn(sh)))

def list_of_rows[shape: (* -> *) -> *](RecordSet(fields, rows, getters, traverse, record_to_list): RecordSet[shape]):
  def getter_to_row_entry(row: shape[RecordValue]):
    (result_fn: forall tt. RecordGetter[shape, tt] -> RecordRowEntry[RecordValue, tt]) = RecordGetter(get_field, get_value) ->
      RecordField(_, to_entry) = get_field(fields)
      RecordRowEntry(to_entry(get_value(row)))
    result_fn
  [record_to_list(getters.traverse()(getter_to_row_entry(row))) for row in rows]

struct RestructureOutput[shape1, shape2](
  reshaper_fields: shape1[RecordField] -> shape2[RecordField],
  reshaper_values: shape1[RecordValue] -> shape2[RecordValue],
  getters: shape2[RecordGetter[shape2]],
  traverse: forall w1: * -> *, w2: * -> *. shape2[w1] -> (forall ss. w1[ss] -> w2[ss]) -> shape2[w2],
  record_to_list: forall w: * -> *. shape2[RecordRowEntry[w]] -> List[RowEntry[w]]
)
def restructure[
  shape1: (* -> *) -> *,
  shape2: (* -> *) -> *
](RecordSet(fields, rows, getters, _, _): RecordSet[shape1], f: shape1[RecordGetter[shape1]] -> RestructureOutput[shape1, shape2]) -> RecordSet[shape2]:
  RestructureOutput(reshaper_f, reshaper_v, new_getters, traverse, record_to_list) = f(getters)
  RecordSet(reshaper_f(fields), rows.map_List(reshaper_v), new_getters, traverse, record_to_list)

def concat_records(RecordSet(fields, rows, getters, traverse, record_to_list), more_rows):
  RecordSet(fields, rows.concat(more_rows), getters, traverse, record_to_list)

struct NilShape[w: * -> *]
struct PS[t,rest,w](left: w[t], right: rest[w])

new_record_set = RecordSet(NilShape, [], NilShape, NilShape -> _ -> NilShape, _ -> [])

(ps_end: forall t: (* -> *) -> *. RestructureOutput[t, NilShape]) = RestructureOutput(
  _ -> NilShape,
  _ -> NilShape,
  NilShape,
  _ -> _ -> NilShape,
  _ -> []
)

def ps[
  shape1: (* -> *) -> *,
  shape2: (* -> *) -> *,
  t
](
  RecordGetter(f_f, f_v): RecordGetter[shape1, t],
  RestructureOutput(reshaper_1_f, reshaper_1_v, getters1, traverse1, record_to_list1): RestructureOutput[shape1, shape2]):
  getters2 = getters1.traverse1()(RecordGetter(f1, v1) -> RecordGetter(PS(_, sh2) -> f1(sh2), PS(_, sh2) -> v1(sh2)))
  RestructureOutput(
    sh1 -> PS(f_f(sh1), reshaper_1_f(sh1)),
    sh1 -> PS(f_v(sh1), reshaper_1_v(sh1)),
    PS(RecordGetter(PS(x,_) -> x, PS(x,_) -> x), getters2),
    PS(x, sh2) -> g -> PS(g(x), sh2.traverse1()(g)),
    PS(RecordRowEntry(row_entry), sh2) -> [row_entry].concat(record_to_list1(sh2))
  )

def string_field[shape: (* -> *) -> *](name, fn: shape[RecordValue] -> String): RecordField(name, REString).create_field(fn)
def int_field[shape: (* -> *) -> *](name, fn: shape[RecordValue] -> Int): RecordField(name, REInt).create_field(fn)
def bool_field[shape: (* -> *) -> *](name, fn: shape[RecordValue] -> Bool): RecordField(name, REBool).create_field(fn)

##################################################

def and(x, y):
  y if x else False

operator && = and

def equals(compare, x, y):
  compare(x,y) matches EQ

def cmp_Bool(x, y):
  match (x, y):
    case (True, False): GT
    case (False, True): LT
    case _: EQ

def equal_List(is_equal, l1, l2):
  recur l1:
    case []: match l2:
      case []: True
      case _: False
    case [h1, *r1]: match l2:
      case []: False
      case [h2, *r2]: is_equal(h1, h2) && equal_List(is_equal, r1, r2)

def equal_RowEntry(re1, re2):
  match (re1, re2):
    case (REBool(RecordValue(x1)), REBool(RecordValue(x2))): cmp_Bool.equals(x1, x2)
    case (REInt(RecordValue(x1)), REInt(RecordValue(x2))): cmp_Int.equals(x1, x2)
    case (REString(RecordValue(x1)), REString(RecordValue(x2))): cmp_String.equals(x1, x2)
    case _: False

equal_rows = (a, b) -> equal_List(equal_RowEntry, a, b)

##################################################

rs_empty = new_record_set.restructure(_ -> ps("String field".string_field(_ -> ""), ps("Int field".int_field(_ -> 0), ps("Bool field".bool_field(_ -> True), ps_end))))

rs = rs_empty.concat_records([PS(RecordValue("a"), PS(RecordValue(1), PS(RecordValue(False), NilShape)))])

rs0 = rs.restructure(PS(a, PS(b, PS(c, _))) -> ps(c, ps(b, ps(a, ps("Plus 2".int_field( r -> r.get(b).add(2) ), ps_end)))))

tests = TestSuite("reordering",
  [
    Assertion(equal_rows.equal_List(rs0.list_of_rows(), [[REBool(RecordValue(False)), REInt(RecordValue(1)), REString(RecordValue("a")), REInt(RecordValue(3))]]), "swap")
  ]
)
