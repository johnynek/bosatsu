package BuildLibrary

export (File(), Library(), LibArgs(), build, Build, file, files, build_all, library, empty,
  map_Build, map2_Build)

struct Leibniz[a, b](cast: forall f: * -> *. f[a] -> f[b])
(refl: forall a. Leibniz[a, a]) = Leibniz(x -> x)

struct File(path: String)
struct Library

enum Build[a: *]:
  LoadFile(name: String, is_file: Leibniz[a, File])
  Pure(value: a)
  Mapped[b](prev: Build[b], fn: b -> a)
  Map2[b, c](left: Build[b], right: Build[c], fn: (b, c) -> a)
  BuildLib(files: Build[List[File]], deps: Build[List[Library]], is_lib: Leibniz[a, Library])

def map_Build(b: Build[a], fn: a -> b) -> Build[b]:
  Mapped(b, fn)

def map2_Build(ba: Build[a], bb: Build[b], fn: (a, b) -> c) -> Build[c]:
  Map2(ba, bb, fn)

def file(s: String) -> Build[File]:
    LoadFile(s, refl)

(empty: forall a. Build[List[a]]) = Pure([])

def build_all(items: List[Build[a]]) -> Build[List[a]]:
    recur items:
      case []: empty
      case [h, *t]:
          (rest: Build[List[a]]) = build_all(t)
          map2_Build(h, rest, (h, t) -> [h, *t])

def files(fs: List[String]) -> Build[List[File]]:
    build_all([file(f) for f in fs])

def library(
    sources: Build[List[File]],
    deps: Build[List[Library]]) -> Build[Library]:
    BuildLib(sources, deps, refl)

struct LibArgs(srcs: Build[List[File]], deps: Build[List[Library]])

def build(args: LibArgs) -> Build[Library]:
  LibArgs { srcs, deps } = args
  library(srcs, deps)
