package Bosatsu/Predef

export [
  Assertion(),
  Bool(),
  Comparison(),
  Int,
  Option(),
  List,
  String,
  Test(),
  TestSuite(),
  add,
  cmp_Int,
  concat,
  consList,
  emptyList,
  eq_Int,
  foldLeft,
  mod_Int,
  range,
  reverse,
  reverse_concat,
  sub,
  times,
  trace,
]

enum Bool:
  False
  True

external struct List[a]

external def emptyList -> List[a]
external def consList(head: a, tail: List[a]) -> List[a]

external def foldLeft(lst: List[a], item: b, fn: b -> a -> b) -> b

external def range(exclusiveUpper: Int) -> List[Int]

def reverse_concat(front: List[a], back: List[a]) -> List[a]:
  foldLeft(front, back, \tail, h -> consList(h, tail))

def reverse(as: List[a]) -> List[a]:
  reverse_concat(as, emptyList)

def concat(front: List[a], back: List[a]) -> List[a]:
  match back:
    []: front
    _: reverse_concat(reverse(front), back)

enum Comparison:
  LT
  EQ
  GT

enum Option:
  None
  Some(v)

external struct Int

external def add(a: Int, b: Int) -> Int
external def sub(a: Int, b: Int) -> Int
external def times(a: Int, b: Int) -> Int
external def eq_Int(a: Int, b: Int) -> Bool
external def cmp_Int(a: Int, b: Int) -> Comparison
external def mod_Int(a: Int, mod: Int) -> Int

external struct String

struct Assertion(value: Bool, message: String)
struct Test(name: String, assertions: List[Assertion])
struct TestSuite(name: String, tests: List[Test])

external def trace(prefix: String, item: a) -> a
